
plugins {
    id 'java'
    id 'maven-publish'
    id 'checkstyle'
}

version = '0.14-SNAPSHOT'
sourceCompatibility = 1.8

def pingdom_sdk_version = '2.1'

defaultTasks 'build', 'check'

if (System.getenv("TRAVIS_BRANCH") == null) {
    check.dependsOn 'publishToMavenLocal'
} else {
    check.dependsOn 'publish'
}

configurations {
    gyroDoclet
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://artifactory.psdops.com/public"
    }
    maven {
        url "https://artifactory.psdops.com/gyro-snapshots"
    }
}

dependencies {
    compile 'com.psddev:dari-util:3.3.607-xe0f27a'

    compile "gyro:gyro-core:0.14-SNAPSHOT"

    checkstyle 'com.puppycrawl.tools:checkstyle:8.15'

    gyroDoclet "gyro:gyro-doclet:0.14-SNAPSHOT"

    compile 'com.squareup.retrofit2:retrofit:2.5.0'
    //compile 'com.squareup.retrofit2:converter-gson'
    compile 'com.google.code.gson:gson:2.8.5'
    compile group: 'com.squareup.retrofit2', name: 'converter-gson', version: '2.5.0'
}


publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'gyro'
            artifactId = 'gyro-pingdom-provider'
            version = version

            from components.java
        }
    }

    repositories {
        maven {
            credentials {
                username System.getenv("GRADLE_DEPLOY_USER")
                password System.getenv("GRADLE_DEPLOY_PASSWORD")
            }

            if (project.version.endsWith('-SNAPSHOT')) {
                url "https://artifactory.psdops.com/gyro-snapshots"
            } else {
                url "https://artifactory.psdops.com/gyro-releases"
            }
        }
    }
}

task referenceDocs(type: Javadoc) {
    title = null // Prevents -doctitle and -windowtitle from being passed to BeamDoclet
    source = sourceSets.main.allJava
    classpath = configurations.compile
    options.doclet = "gyro.doclet.GyroDoclet"
    options.docletpath = configurations.gyroDoclet.files.asType(List)
}

checkstyle {
    toolVersion = '6.13'
    configFile rootProject.file('config/checkstyle.xml')
    checkstyleMain.enabled = false
    checkstyleTest.enabled = false
}

task checkstyle(type: Checkstyle) {
    configFile rootProject.file('config/checkstyle.xml')
    source 'src'
    include '**/*.java'
    exclude '**/generated-src/**'

    classpath = files()
}
